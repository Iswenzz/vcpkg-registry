if(VCPKG_TARGET_ARCHITECTURE STREQUAL "x86")
	vcpkg_download_distfile(ARCHIVE
		URLS https://cef-builds.spotifycdn.com/cef_binary_142.0.17%2Bg60aac24%2Bchromium-142.0.7444.176_windows32.tar.bz2
		FILENAME "cef.x86.tar.bz2"
		SHA512 36b4105c9734aed4d15eae2b9da37f7196376c5f4dd91f1ae8b09b8204f16d44f4200e5f210e193b81223cc49a286eb35e82fd444f817a7edb1465a6b6088b72
	)
elseif(VCPKG_TARGET_ARCHITECTURE STREQUAL "x64")
	vcpkg_download_distfile(ARCHIVE
		URLS https://cef-builds.spotifycdn.com/cef_binary_142.0.17%2Bg60aac24%2Bchromium-142.0.7444.176_windows64.tar.bz2
		FILENAME "cef.x64.tar.bz2"
		SHA512 c9080ae1f69472a55e96736b58a002404fe2973327b2b580484dd5002c26408f1a0c5abdc907eb93e277b8d995285e3f87b94586ff916d218e39ea03f8e6c66f
	)
else()
	message(FATAL_ERROR "CEF does not support architecture: ${VCPKG_TARGET_ARCHITECTURE}")
endif()

vcpkg_extract_source_archive_ex(
	OUT_SOURCE_PATH SOURCE_PATH
	ARCHIVE ${ARCHIVE}
)

set(PREV_VCPKG_LIBRARY_LINKAGE ${VCPKG_LIBRARY_LINKAGE})
set(VCPKG_POLICY_SKIP_ARCHITECTURE_CHECK enabled)
set(VCPKG_POLICY_SKIP_DUMPBIN_CHECKS enabled)
set(VCPKG_LIBRARY_LINKAGE static)

set(CEF_CONFIG_OPTS -DUSE_SANDBOX=NO)
if("${VCPKG_CRT_LINKAGE}" STREQUAL "dynamic")
	list(APPEND CEF_CONFIG_OPTS -DCEF_RUNTIME_LIBRARY_FLAG=/MD)
endif()

file(COPY ${CMAKE_CURRENT_LIST_DIR}/CMakeLists.txt DESTINATION ${SOURCE_PATH})

vcpkg_cmake_configure(
	SOURCE_PATH "${SOURCE_PATH}"
	OPTIONS ${CEF_CONFIG_OPTS})

set(VCPKG_LIBRARY_LINKAGE ${PREV_VCPKG_LIBRARY_LINKAGE})

vcpkg_cmake_install()
vcpkg_copy_pdbs()

file(INSTALL "${SOURCE_PATH}/include" DESTINATION "${CURRENT_PACKAGES_DIR}")

file(GLOB DLLS "${SOURCE_PATH}/Release/*.dll")
file(INSTALL ${DLLS} DESTINATION "${CURRENT_PACKAGES_DIR}/bin")
file(GLOB LIBS "${SOURCE_PATH}/Release/*.lib")
file(INSTALL ${LIBS} DESTINATION "${CURRENT_PACKAGES_DIR}/lib")
file(GLOB BIN_FILES "${SOURCE_PATH}/Release/*.bin")
file(INSTALL ${BIN_FILES} DESTINATION "${CURRENT_PACKAGES_DIR}/bin")
file(GLOB JSON_FILES "${SOURCE_PATH}/Release/*.json")
file(INSTALL ${JSON_FILES} DESTINATION "${CURRENT_PACKAGES_DIR}/bin")

if(VCPKG_BUILD_TYPE STREQUAL "debug" OR NOT DEFINED VCPKG_BUILD_TYPE)
	file(GLOB DEBUG_DLLS "${SOURCE_PATH}/Debug/*.dll")
	file(INSTALL ${DEBUG_DLLS} DESTINATION "${CURRENT_PACKAGES_DIR}/debug/bin")
	file(GLOB DEBUG_LIBS "${SOURCE_PATH}/Debug/*.lib")
	file(INSTALL ${DEBUG_LIBS} DESTINATION "${CURRENT_PACKAGES_DIR}/debug/lib")
	file(GLOB DEBUG_BIN_FILES "${SOURCE_PATH}/Debug/*.bin")
	file(INSTALL ${DEBUG_BIN_FILES} DESTINATION "${CURRENT_PACKAGES_DIR}/debug/bin")
	file(GLOB DEBUG_JSON_FILES "${SOURCE_PATH}/Debug/*.json")
	file(INSTALL ${DEBUG_JSON_FILES} DESTINATION "${CURRENT_PACKAGES_DIR}/debug/bin")
endif()

file(INSTALL "${SOURCE_PATH}/Resources" DESTINATION "${CURRENT_PACKAGES_DIR}/share/${PORT}")
file(INSTALL "${SOURCE_PATH}/LICENSE.txt" DESTINATION "${CURRENT_PACKAGES_DIR}/share/${PORT}" RENAME copyright)
file(INSTALL "${CMAKE_CURRENT_LIST_DIR}/usage" DESTINATION "${CURRENT_PACKAGES_DIR}/share/${PORT}")
